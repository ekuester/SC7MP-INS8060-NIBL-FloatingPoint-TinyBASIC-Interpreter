1 REM *** KALENDA.BAS ***
2 REM *** AUTOR: ERICH KUESTER (C) 1975 - 2023 ***
4 REM *** ERSTES PROGRAMM IN FORTRAN IV AUF CYBER CDC 7200 ***
5 REM *** ANGEPASSTER QUELLCODE FUER NIBLFP AUF SC/MP ***
8 REM TAGE IN JEDEM MONAT
10 DATA 31,28,31,30,31,30,31,31,30,31,30,31
11 REM ALLOCATE SPACE FOR STRINGS AND ARRAYS 
12 REM ANZAHL BYTES FUER STRING: LAENGE PLUS 1,
13 REM FUER DIM FELDERZAHL*4 PLUS 6
14 TG=TOP: TG$="MO DI MI DO FR SA SO "
16 MON=TOP+22: DIM MON(12): WTE=TOP+76: DIM WTE(12)
20 TAG=TOP+130
22 TAG$=" 1  2  3  4  5  6  7  8  9 10 11 12 13 14 15 16 17 18 19 "
23 REM KALTE VERKNUEPFUNG, ZWEI STRING ZU EINEM
24 TAG1=TAG+LEN(TAG$)
26 TAG1$="20 21 22 23 24 25 26 27 28 29 30 31 "
28 REM STRING TAG$ IST JETZT 57 + 36 = 93 BYTES LANG
30 REM AUSGABEZEILE
32 LINE=TAG+94: LINE$="                     "
100 INPUT"WELCHES JAHR" JAHR
110 A=INT(JAHR/100)
112 B=JAHR MOD 1000
114 C=JAHR MOD 100
116 D=JAHR MOD 4
118 SCHALT=0
120 IF D=0 THEN SCHALT=1
122 E=A MOD 4
123 REM KEIN SCHALTJAHR
124 IF (C=0)AND(E<>0) THEN SCHALT=0
125 IF SCHALT=0 THEN PR " KEIN";: ELSE PR " EIN";
126 PR " SCHALTJAHR"
127 F=A
128 IF A>19 THEN F=19
130 I=2*(19-F)
132 F=JAHR-100*F
134 J=JAHR
136 K=4
140 S=J MOD 10
142 REM GOSUB JANA(K,S)
144 J=INT(J/10)
146 K=K-1
148 IF K<>0 THEN GOTO 140
150 REM WOCHENTAG
152 WT=INT(F+F/4+I)
154 WT=WT+1
156 REM WOCHENTAG FUER DEN 1. TAG, TAGE IN JEDEM MONAT
158 FOR M=1 TO 12
160 WT=WT MOD 7: IF WT=0 THEN WT=7
162 WTE(M)=WT
164 READ T
166 IF (M=2) THEN FEBR=T: IF SCHALT=1 THEN T=T+1: FEBR=T
168 MON(M)=T
170 WT=WT+T
174 NEXT M
178 GOTO 600
180 END
600 PR "A HAPPY NEW YEAR ..."
640 PR " ", JAHR
650 FOR K=1 TO 12 STEP 3
652 GOSUB 840
654 FOR L=1 TO 3
656 REM NAME DER WOCHENTAGE DRUCKEN
658 PR " ", TG$;
660 NEXT L
662 PR ""
664 GOSUB 1100
668 PR ""
670 NEXT K
714 PR SPC(40), "LET'S HOPE, IT'S A GOOD ONE"
720 PR SPC(51), "WITHOUT ANY FEAR"
800 END
840 REM SUBROUTINE MONATSNAMEN DRUCKEN
850 IF K=1 THEN PR"JANUAR", SPC(16), "FEBRUAR", SPC(15), "MAERZ"
860 IF K=4 THEN PR"APRIL", SPC(17), "MAI", SPC(19), "JUNI"
870 IF K=7 THEN PR"JULI", SPC(18), "AUGUST", SPC(16), "SEPTEMBER"
880 IF K=10 THEN PR"OKTOBER", SPC(15), "NOVEMBER", SPC(14), "DEZEMBER"
890 RETURN
1100 REM SUBROUTINE ZEILEN DRUCKEN
1109 REM ERSTE REIHE
1110 ANF1=WTE(K)*3-2: R1=22-ANF1: LINE$=MID$(TAG$,1, R1)
1120 PR SPC(ANF1), LINE$;
1130 ANF2=WTE(K+1)*3-2: R2=22-ANF2: LINE$=MID$(TAG$, 1, R2)
1140 PR SPC(ANF2), LINE$;
1150 ANF3=WTE(K+2)*3-2: R3=22-ANF3: LINE$=MID$(TAG$,1, R3)
1160 PR SPC(ANF3), LINE$
1162 R1=R1+1: R2=R2+1: R3=R3+1
1168 REM ZWEITE BIS VIERTE REIHE
1169 FOR R=1 TO 3
1170 LINE$=MID$(TAG$,R1,21)
1180 PR SPC(1), LINE$;
1190 LINE$=MID$(TAG$,R2,21)
1200 PR SPC(1), LINE$;
1210 LINE$=MID$(TAG$,R3,21)
1220 PR SPC(1), LINE$
1222 R1=R1+21: R2=R2+21: R3=R3+21
1288 NEXT R
1349 REM FUENFTE REIHE: MONATSENDE NAHT
1350 T1=MON(K)*3: DIFF=T1-R1
1360 IF DIFF>20 THEN LINE$=MID$(TAG$,R1,21): S1=0: PR SPC(1), LINE$;
1370 IF DIFF<=20 THEN LINE$=MID$(TAG$,R1,DIFF): S1=22: GOSUB 1600
1380 T2=MON(K+1)*3: DIFF=T2-R2
1390 IF DIFF>20 THEN LINE$=MID$(TAG$,R2,21): S2=0: PR SPC(1), LINE$;
1400 IF DIFF<=20 THEN LINE$=MID$(TAG$,R2,DIFF): S2=22: GOSUB 1600
1410 T3=MON(K+2)*3: DIFF=T3-R3
1420 IF DIFF>20 THEN LINE$=MID$(TAG$,R3,21): S3=0: PR SPC(1), LINE$;
1430 IF DIFF<=20 THEN LINE$=MID$(TAG$,R3,DIFF): S3=22: GOSUB 1600
1440 PR ""
1449 REM SECHSTE REIHE: MONATSENDE AUF JEDEN FALL
1450 R1=R1+21: T1=MON(K)*3: DIFF=T1-R1
1452 IF S1>0 THEN PR SPC(S1);: GOTO 1460
1454 LINE$=MID$(TAG$,R1,DIFF): GOSUB 1600
1460 R2=R2+21: T2=MON(K+1)*3: DIFF=T2-R2
1462 IF S2>0 THEN PR SPC(S2);: GOTO 1470
1464 LINE$=MID$(TAG$,R2,DIFF): GOSUB 1600
1470 R3=R3+21: T3=MON(K+2)*3: DIFF=T3-R3
1472 IF S3>0 THEN PR SPC(S3): GOTO 1500
1474 LINE$=MID$(TAG$,R3,DIFF): GOSUB 1600
1480 PR ""
1500 RETURN
1600 PR SPC(1), LINE$, SPC(21-DIFF);
1610 RETURN

